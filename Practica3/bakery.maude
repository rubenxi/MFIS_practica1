load model-checker.maude

omod BAKERY is
    pr NAT .
   
    class Dispenser | next : Nat, last : Nat .
    sorts Mode GBState .
    
    class BProcess | mode : Mode, number : Nat .
    ops sleep wait crit : -> Mode [ctor] .
    op id : Nat -> Oid .

    op [[_]] : Configuration -> GBState .

    vars O Z : Oid .
    vars N M T : Nat .
    vars ATTS ATTS' : AttributeSet .
   
    rl [waitToCrit] : < O : BProcess |  mode : wait, number : N > < Z : Dispenser | next : N, ATTS >  =>
                    < O : BProcess |  mode : crit, number : N > < Z : Dispenser | next : N, ATTS > .

    rl [critToSleep] :  < O : BProcess |  mode : crit, number : N >  < Z : Dispenser | next : N, ATTS > =>
                    < O : BProcess |  mode : sleep, number : 0 >  < Z : Dispenser | next : s(N), ATTS >  .

    rl [sleepToWait] : < O : BProcess |  mode : sleep, number : N >  < Z : Dispenser | next : T, last : M > =>
                    < O : BProcess |  mode : wait, number : M >  < Z : Dispenser | next : T, last : s(M) >  .

    op initial : Nat -> GBState .
    eq initial(N) = [[$initial(N)]] .

    op $initial : Nat -> Configuration .
    eq $initial(0) =  < id(0) : Dispenser | next : s 0, last : s 0 > .
    eq $initial(s N) = < id(s N) : BProcess |  mode : sleep, number : 0 > $initial(N) .
    
endom

mod ABSTRACT-BAKERY is
    extending BAKERY .
    var O : Oid .
    var N : Nat .
    var M : Mode .
    ceq < O : BProcess | mode : M, number : s(N) > = < O : BProcess | mode : M, number : N > if M == sleep = false .
endm

mod ABSTRACT-BAKERY-PREDS is
    protecting ABSTRACT-BAKERY .
    including SATISFACTION .
    subsort GBState < State .
    
    var MODE : Mode .
    var G : GBState .
    vars N M : Nat .
    vars O O' : Oid .
    var C : Configuration .
    op mode : Nat Mode -> Prop . 
    eq [[ < O : BProcess | mode : MODE, number : N > C ]] |= mode(N, MODE) = true .
    op 2-crit : -> Prop . 
    eq [[ < O : BProcess |  mode : crit,  number : N > < O' : BProcess |  mode : crit,  number : M > C ]] |= 2-crit = true .
endm

mod ABSTRACT-BAKERY-CHECK is
    protecting ABSTRACT-BAKERY-PREDS . 
    including MODEL-CHECKER . 
    including LTL-SIMPLIFIER .
endm
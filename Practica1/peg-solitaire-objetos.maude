load prelude.maude

mod PEG-SOLITAIRE-OBJETOS is
    protecting NAT .

    sort Casilla Tablero .
    subsort Casilla < Tablero .

    ***Creación casilla con ficha y casilla vacía
    op __ : Tablero Tablero -> Tablero [assoc comm ctor] .
    op o : Nat Nat -> Casilla [ctor] .
    op free : Nat Nat -> Casilla [ctor] .

    vars T1 T2 : Tablero .
    vars M N : Nat .

    ***Para saber si el juego ha terminado, se cuenta el número de fichas en el tablero.
    ***Si tan solo hay una ficha en el tablero, se habrá encontrado una solución

    op contarFichas : Tablero -> Nat .
    eq contarFichas (o(M,N)) = 1 .
    eq contarFichas (free(M,N)) = 0 .
    eq contarFichas (T1 T2) = contarFichas(T1) + contarFichas(T2) .

    op cuatro : -> Tablero .
    eq cuatro =         o(0,0) o(0,1) o(0,2) o(0,3)
                        o(1,0) o(1,1) o(1,2) o(1,3)
                        o(2,0) o(2,1) o(2,2) o(2,3)
                        o(3,0) o(3,1) free(3,2) o(3,3) .

    op cuatro22 : -> Tablero .
    eq cuatro22 =        o(0,0) o(0,1) o(0,2) o(0,3)
                        o(1,0) o(1,1) o(1,2) o(1,3)
                        o(2,0) o(2,1) free(2,2) o(2,3)
                        o(3,0) o(3,1) o(3,2) o(3,3) .

    op cuatro33 : -> Tablero .
    eq cuatro33 =       o(0,0) o(0,1) o(0,2) o(0,3)
                        o(1,0) o(1,1) o(1,2) o(1,3)
                        o(2,0) o(2,1) o(2,2) o(2,3)
                        o(3,0) o(3,1) o(3,2) free(3,3) .

    op cinco : -> Tablero .
    eq cinco =          o(0,0) o(0,1) o(0,2) o(0,3) o(0,4)
                        o(1,0) o(1,1) o(1,2) o(1,3) o(1,4)
                        o(2,0) o(2,1) o(2,2) free(2,3) o(2,4)
                        o(3,0) o(3,1) o(3,2) o(3,3) o(3,4) 
                        o(4,0) o(4,1) o(4,2) o(4,3) o(4,4) .


    ***Reglas

    rl [derecha] : o(M,N) o(M, s N) free(M, s s N) => free(M,N) free(M, s N) o(M, s s N) .
    rl [izquierda] : free(M,N) o(M, s N) o(M, s s N) => o(M,N) free(M, s N) free(M, s s N) .
    rl [abajo] : o(M,N) o(s M, N) free(s s M, N) => free(M,N) free(s M, N) o(s s M, N) .
    rl [arriba] : free(M,N) o(s M, N) o(s s M, N) => o(M,N) free(s M, N) free(s s M, N) .

    endm

    

    search in PEG-SOLITAIRE-OBJETOS : cuatro =>* T1 such that contarFichas(T1) = 1 .
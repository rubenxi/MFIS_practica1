load bakery.maude
load model-checker.maude

omod BAKERY+ is
    extending BAKERY .

    var BPid Did : Oid .
    vars N M : Nat .
    vars ATTS ATTS' : AttributeSet .
    var C : Configuration .
    var MODE : Mode .
    

    op processWithNumber : Configuration Nat -> Bool .
    eq processWithNumber(none, N) = false .
    eq processWithNumber(< BPid : BProcess |  mode : MODE, number : N > C, N) = true or processWithNumber(C, N) .
    eq processWithNumber(< Did : Dispenser | ATTS > C, N) = processWithNumber(C, N) .
    ceq processWithNumber(< BPid : BProcess |  mode : MODE, number : M > C, N) = processWithNumber(C, N) if M =/= N .


    rl [wait-sleep] : < BPid : BProcess |  mode : wait, number : N >  => < BPid : BProcess |  mode : sleep, number : 0 > .

    crl [skipRound] : [[ < Did : Dispenser | next : N, ATTS > < BPid : BProcess | mode : wait, ATTS' > C ]]  => [[ < Did : Dispenser | next : s N, ATTS > < BPid : BProcess | mode : wait, ATTS' > C ]]
                    if not(processWithNumber(< BPid : BProcess | mode : wait, ATTS' > C, N)) .
endom

mod ABSTRACT-BAKERY+ is
    including BAKERY+ .
    including ABSTRACT-BAKERY .

    vars Did BPid : Oid .
    vars N M : Nat .
    var C : Configuration .
    var MODE : Mode .

    ceq [[ < Did : Dispenser | next : s(0), last : s(M) > < BPid : BProcess | mode : MODE, number : s(N) > C  ]] =
        [[ < Did : Dispenser | next : s(0), last : M > < BPid : BProcess | mode : MODE, number : N > C ]] 
        if not(processWithNumber(C, N)) .
endm

mod ABSTRACT-BAKERY-CHECK+ is
    protecting ABSTRACT-BAKERY-PREDS . 
    including MODEL-CHECKER . 
    including LTL-SIMPLIFIER .
endm